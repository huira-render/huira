@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Add installed find modules to module path
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

# Find GLM
find_dependency(glm CONFIG QUIET)
if(NOT glm_FOUND)
    find_path(GLM_INCLUDE_DIR glm/glm.hpp REQUIRED)
    add_library(glm::glm INTERFACE IMPORTED)
    target_include_directories(glm::glm INTERFACE ${GLM_INCLUDE_DIR})
endif()

find_dependency(CSPICE)

find_dependency(assimp CONFIG)

find_dependency(TBB CONFIG)

find_dependency(JPEG)
find_dependency(PNG)
find_package(CFITSIO REQUIRED)
# Conda-forge's cfitsio includes 'm' in its link interface, which doesn't
# exist on Windows (math functions are in the CRT). Remove it.
if(WIN32 AND TARGET CFITSIO::CFITSIO)
    get_target_property(_cfitsio_libs CFITSIO::CFITSIO INTERFACE_LINK_LIBRARIES)
    if(_cfitsio_libs)
        list(REMOVE_ITEM _cfitsio_libs "m")
        set_target_properties(CFITSIO::CFITSIO PROPERTIES INTERFACE_LINK_LIBRARIES "${_cfitsio_libs}")
    endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/huiraTargets.cmake")
check_required_components(huira)
